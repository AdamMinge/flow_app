# ----------------------------------------------------------------------- #
# ------------------- Check that called with component ------------------ #
# ----------------------------------------------------------------------- #
if (NOT pillar_FIND_COMPONENTS)
    message(FATAL_ERROR "find_package(pillar) called with no component")
endif()
# ----------------------------------------------------------------------- #
# -------------- Set config_name dependent on libraries type ------------ #
# ----------------------------------------------------------------------- #
if(PILLAR_STATIC_LIBRARIES)
    set(config_name "static")
else()
    set(config_name "shared")
endif()
# ----------------------------------------------------------------------- #
# ---------------------- Update requested components -------------------- #
# ----------------------------------------------------------------------- #
set(FIND_pillar_ADDITIONAL_COMPONENTS "")

foreach(component ${pillar_FIND_COMPONENTS})
    list(APPEND FIND_pillar_ADDITIONAL_COMPONENTS ${FIND_pillar_${component}_DEPENDENCIES})
endforeach()

list(APPEND pillar_FIND_COMPONENTS ${FIND_pillar_ADDITIONAL_COMPONENTS})
list(REMOVE_DUPLICATES pillar_FIND_COMPONENTS)
# ----------------------------------------------------------------------- #
# -------------------- Load targets and dependencies -------------------- #
# ----------------------------------------------------------------------- #
set(targets_config_file ${CMAKE_CURRENT_LIST_DIR}/pillar-${config_name}-targets.cmake)
set(pillar_FOUND TRUE)

if(EXISTS ${targets_config_file})
    include(${targets_config_file})
    include("${CMAKE_CURRENT_LIST_DIR}/pillar-config-dependencies.cmake")

    if (pillar_FOUND)
        foreach (component ${pillar_FIND_COMPONENTS})
            if (TARGET pillar-${component})
                set(pillar_${component}_FOUND TRUE)
            else()
                set(FIND_pillar_ERROR "Found pillar but requested component '${component}' is missing in the config defined in ${PILLAR_DIR}.")
                set(pillar_${component}_FOUND FALSE)
                set(pillar_FOUND FALSE)
            endif()
        endforeach()
    endif()
else()
    set(FIND_pillar_ERROR "Requested pillar configuration (${config_name}) was not found")
    set(pillar_FOUND FALSE)
endif()
# ----------------------------------------------------------------------- #
# ------------=------------- Display all errors ------------------------- #
# ----------------------------------------------------------------------- #
if (NOT pillar_FOUND)
    if(pillar_FIND_REQUIRED)
        message(FATAL_ERROR "${FIND_pillar_ERROR}")
    elseif(NOT pillar_FIND_QUIETLY)
        message(STATUS "${FIND_pillar_ERROR}")
    endif()
endif()
# ----------------------------------------------------------------------- #
